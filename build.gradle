buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.4"
    }
}

allprojects {
    group 'com.austinv11.rx'
    version '1.0'

    task wrapper(type: Wrapper) {
        gradleVersion = '3.4.1'
        distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        jcenter()
        maven {
            url  "https://jitpack.io"
        }
        maven { url 'http://repo.spring.io/milestone' }
    }

    dependencies {
        compileOnly "com.github.austinv11:Discord4J:2.8.1"
        compile 'org.reactivestreams:reactive-streams:1.0.0'

        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
        archives shadowJar
    }

    compileJava {
        options.compilerArgs << '-XDignore.symbol.file'
        options.fork = true
        options.forkOptions.executable = 'javac'
    }

    javadoc {
        failOnError false
    }

    task copyJars(type: Copy) {
        from jar.destinationDir
        into("${rootProject.buildDir}/jars/")
    }

    jar.finalizedBy copyJars

}
